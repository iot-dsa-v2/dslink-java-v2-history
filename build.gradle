apply plugin: 'application'
apply plugin: 'java-library'

// Version is taken from dslink.json
build {
    version = new groovy.json.JsonSlurper().parseText(file('dslink.json').text).version
}

group 'org.iot-dsa'
mainClassName = 'org.iot.dsa.dslink.DSLink'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
    mavenLocal()
    maven {
        url 'https://jitpack.io'
    }
}

dependencies {
    //api 'org.iot-dsa:dslink-v2-websocket:+' //for a locally installed sdk
    api 'com.github.iot-dsa-v2.sdk-dslink-java-v2:dslink-v2-websocket:0.70.0'
    testImplementation 'org.testng:testng:6+'
}

jar {
    manifest {
        attributes(
                'Automatic-Module-Name' : 'org.iot.dsa.' + project.name.replaceAll('-','.')
        )
    }
}

applicationDistribution.from(new File(project.projectDir, "/dslink.json"))

run {
    args System.getProperty("exec.args", "").split()
    workingDir project.buildDir
}

test {
    useTestNG()
}

wrapper {
    gradleVersion = '5.6.2'
}
